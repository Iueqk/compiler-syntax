程序  ->  函数
形参  ->  返回值类型 id  形参'
形参  -> @
形参'  -> ,  形参
形参'  -> @
返回值类型  -> int
返回值类型  -> void
返回值类型  -> bool
返回值类型  -> char
函数  ->  返回值类型 id (  形参 ) { 函数主体 }  函数
函数  -> @
函数主体  ->  处理语句  返回语句
变量申请  ->  返回值类型 id  赋初值 ;
赋初值 -> = 赋初值内容 赋初值
赋初值 -> , id 赋初值
赋初值  -> @
赋初值内容 -> 算数表达式
赋初值内容 -> ch
赋初值内容 -> 正确或者错误
正确或者错误 -> true
正确或者错误 -> false
处理语句 ->   变量申请  处理语句
处理语句  ->  赋值语句与函数调用 ;  处理语句
处理语句  ->  判断语句  处理语句
处理语句  ->  循环语句  处理语句
处理语句  -> @
赋值语句与函数调用  -> id 赋值语句
赋值语句与函数调用  -> 单目运算符 id
赋值语句与函数调用  -> printf ( string 输出的变量
赋值语句与函数调用  -> scanf ( string , 输入变量 )
输出的变量 -> , 变量 )
输出的变量 -> )
变量  ->  算数表达式  变量'
变量'  -> ,  算数表达式  变量'
变量'  -> @
输入变量  ->  input-id 输入变量2
输入变量2  -> ,  input-id 输入变量2
输入变量2 -> @
赋值语句  ->  = 算数表达式
赋值语句  ->  mul-operator 算数表达式
赋值语句  ->  单目运算符
赋值语句  ->  ( 变量 )
数字 -> num
数字 -> - num
整体变量 -> 数字
整体变量  -> id 函数调用或运算
函数调用或运算 -> ( 变量 )
函数调用或运算 -> @
算数表达式 -> 因子 加减复合运算
加减复合运算 -> 低级运算符 因子 加减复合运算
加减复合运算 -> @
因子 -> 项 乘除取模复合运算
乘除取模复合运算 -> higher-operator 项 乘除取模复合运算
乘除取模复合运算 -> @
项 -> ( 算数表达式 )
项 -> 整体变量
低级运算符 -> +
低级运算符 -> -
单目运算符  -> ++
单目运算符  -> --
判断语句  -> if (  条件 ) {  函数主体 }  其他判断语句
其他判断语句  -> else  子判断语句
其他判断语句  -> @
判断 -> 算数表达式 判断运算符 算数表达式
判断 -> ! ( 判断 )
条件 -> 判断 子判断条件
子判断条件 -> and 判断 子判断条件
子判断条件 -> or  判断 子判断条件
子判断条件 -> @
子判断语句  -> if ( 条件 ) {  函数主体 }  其他判断语句
子判断语句  -> { 函数主体 }
判断运算符  -> equal-sign
判断运算符 -> single-judge
循环语句  -> while (  条件 ) { 处理语句 }
循环语句  -> do { 处理语句 } while ( 条件 ) ;
for第一段 -> 变量申请
for第一段 -> 赋值语句与函数调用 ;
循环语句  -> for (  for第一段   条件 ;  赋值语句与函数调用 ) { 处理语句 }
返回语句  -> return  算数表达式 ;
返回语句 -> @